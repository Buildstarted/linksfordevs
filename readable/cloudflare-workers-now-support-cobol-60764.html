<!DOCTYPE html>
<html lang="en">
<head>
    <title>
Cloudflare Workers Now Support COBOL - linksfor.dev(s)    </title>
    <meta charset="utf-8">
    <link rel="alternate" type="application/rss+xml" title="Linksfor.dev(s) feed" href="https://linksfor.dev/feed.rss" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <meta name="google" value="notranslate">
    <link rel="stylesheet" type="text/css" href="/style.css" />
    <link rel="shortcut icon" href="/favicon.ico" type="image/x-icon">
    <link rel="icon" href="/favicon.ico" type="image/x-icon">
    <meta property="og:title" content="Cloudflare Workers Now Support COBOL - linksfor.dev(s)"/>
    <meta property="article:author" content="John Graham-Cumming"/>
    <meta property="og:description" content="COBOL is often a source of amusement for programmers because it is seen as old, verbose, clunky, and difficult to maintain. COBOL can now be used to write code for Cloudflare&#x2019;s serverless platform Workers."/>
    <meta property="og:type" content="website"/>
    <meta property="og:url" content="https://blog.cloudflare.com/cloudflare-workers-now-support-cobol/"/>

<meta property="og:site_name" content="linksfor.dev(s)" />
</head>
<body>
    <div class="devring" style="background: #222">
        <div style="text-align:center">Explore other dev related sites in this ring. If you would like to join this ring <a href="https://devring.club">click here</a>.</div>
        <div class="grid">
            <div style="display: grid; grid-template-columns: .5fr 1fr 1fr 1fr; text-align: center;">
                <span class="devring-title"><a href="https://devring.club/">devring.club</a></span>
                <a href="https://devring.club/sites/1/prev" class="devring-previous">Previous</a>
                <a href="https://devring.club/random" class="devring-random">Random</a>
                <a href="https://devring.club/sites/1/next" class="devring-next">Next</a>
            </div>
        </div>
    </div>
    <div class="grid">
        <h1>
<a href="/" style="color:inherit">linksfor.dev(s)</a>
        </h1>
        <title>linksfor.dev(s) - Cloudflare Workers Now Support COBOL</title>
<div class="readable">
        <h1>Cloudflare Workers Now Support COBOL</h1>
            <div>by John Graham-Cumming</div>
            <div>Reading time: 14-17 minutes</div>
        <div>Posted here: 16 Apr 2020</div>
        <p><a href="https://blog.cloudflare.com/cloudflare-workers-now-support-cobol/">https://blog.cloudflare.com/cloudflare-workers-now-support-cobol/</a></p>
        <hr/>
<div id="readability-page-1" class="page"><section>
            <div>
                <p>Recently, COBOL has been in the news as the State of New Jersey has <a href="https://slate.com/technology/2020/04/new-jersey-unemployment-cobol-coronavirus.html" target="_blank">asked for help with</a> a COBOL-based system for unemployment claims. The system has come under heavy load because of the societal effects of the SARS-CoV-2 virus. This appears to have prompted IBM to offer <a href="https://newsroom.ibm.com/2020-04-09-IBM-and-Open-Mainframe-Project-Mobilize-to-Connect-States-with-COBOL-Skills" target="_blank">free online COBOL training</a>.</p><figure><img src="https://blog-cloudflare-com-assets.storage.googleapis.com/2020/04/report-cropped.png"></figure><p>As old as COBOL is (60 years old this month), it is still heavily used in information management systems and pretty much anywhere there’s an IBM mainframe around. Three years ago Thomson Reuters <a href="http://fingfx.thomsonreuters.com/gfx/rngs/USA-BANKS-COBOL/010040KH18J/index.html" target="_blank">reported</a> that COBOL is used in 43% of banking systems, is behind 80% of in-person financial transactions and 95% of times an ATM card is used. They also reported 100s of billions of lines of running COBOL.</p><p>COBOL is often a source of amusement for programmers because it is seen as old, verbose, clunky, and difficult to maintain. And it’s often the case that people making the jokes have never actually written any COBOL. We plan to give them a chance: COBOL can now be used to write code for Cloudflare’s serverless platform Workers.</p><p>Here’s a simple “Hello, World!” program written in COBOL and accessible at <a href="https://hello-world.cobol.workers.dev/" target="_blank">https://hello-world.cobol.workers.dev/</a>. It doesn’t do much--it just outputs “Hello, World!”--but it does it using COBOL.</p><!--kg-card-begin: markdown--><pre><code>        IDENTIFICATION DIVISION.
        PROGRAM-ID. HELLO-WORLD.
        DATA DIVISION.
        WORKING-STORAGE SECTION.
        01 HTTP_OK   PIC X(4)  VALUE "200".
        01 OUTPUT_TEXT PIC X(14) VALUE "Hello, World!".
        PROCEDURE DIVISION.
            CALL "set_http_status" USING HTTP_OK.
            CALL "append_http_body" USING OUTPUT_TEXT.
        STOP RUN.
</code></pre>
<!--kg-card-end: markdown--><p>If you’ve never seen a COBOL program before, it might look very odd. The language emerged in 1960 from the work of a committee designing a language for business (COBOL = COmmon Business Oriented Language) and was intended to be easy to read and understand (hence the verbose syntax). It was partly based on an early language called <a href="https://en.wikipedia.org/wiki/FLOW-MATIC" target="_blank">FLOW-MATIC</a> created by Grace Hopper.</p><h3 id="identification-division-">IDENTIFICATION DIVISION.</h3><p>To put COBOL in context: FORTRAN arrived in 1957, LISP and ALGOL in 1958, APL in 1962 and BASIC in 1964. The C language didn’t arrive on scene until 1972. The late 1950s and early 1960s saw a huge amount of work on programming languages, some coming from industry (such as FORTRAN and COBOL) and others from academia (such as LISP and ALGOL).</p><p>COBOL is a compiled language and can easily be compiled to WebAssembly and run on Cloudflare Workers. If you want to get started with COBOL, the <a href="https://ftp.gnu.org/gnu/gnucobol/" target="_blank">GNUCobol</a> project is a good place to begin.</p><p>Here’s a program that waits for you to press ENTER and then adds up the numbers 1 to 1000 and outputs the result:</p><!--kg-card-begin: markdown--><pre><code>       IDENTIFICATION DIVISION.
       PROGRAM-ID. ADD.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77 IDX  PICTURE 9999.
       77 SUMX PICTURE 999999.
       77 X    PICTURE X.
       PROCEDURE DIVISION.
       BEGIN.
           ACCEPT X.
           MOVE ZERO TO IDX.
           MOVE ZERO TO SUMX.
           PERFORM ADD-PAR UNTIL IDX = 1001.
           DISPLAY SUMX.
           STOP RUN.
       ADD-PAR.
           COMPUTE SUMX = SUMX + IDX.
           ADD 1 TO IDX.
</code></pre>
<!--kg-card-end: markdown--><p>You can compile it and run it using GNUCobol like this (I put this in a file called <code>terminator.cob</code>)</p><!--kg-card-begin: markdown--><pre><code>$ cobc -x terminator.cob
$ ./terminator
500500
$
</code></pre>
<!--kg-card-end: markdown--><p><code>cobc</code> compiles the COBOL program to an executable file. It can also output a C file containing C code to implement the COBOL program:</p><p><code>$ cobc -C -o terminator.c -x terminator.cob</code></p><p>This <code>.c</code> file can then be compiled to WebAssembly. I’ve done that and placed this program (with small modifications to make it output via HTTP, as in the Hello, World! program above) at <a href="https://terminator.cobol.workers.dev/" target="_blank">https://terminator.cobol.workers.dev/</a>. Note that the online version doesn’t wait for you to press ENTER, it just does the calculation and gives you the answer.</p><h3 id="data-division-">DATA DIVISION.</h3><p>You might be wondering why I called this <code>terminator.cob</code>. That’s because this is part of the code that appears in The Terminator, James Cameron’s 1984 film. The film features a ton of code from the Apple ][ and a little snippet of COBOL (see the screenshot from the film below).</p><figure><img src="https://blog-cloudflare-com-assets.storage.googleapis.com/2020/04/image2-14.png"></figure><p>The screenshot shows the view from one of the HK-Aerial hunter-killer VTOL craft used by Skynet to try to wipe out the remnants of humanity. Using COBOL.</p><p>You can learn all about that in this YouTube video I produced:</p><figure><div><p><iframe src="https://www.youtube.com/embed/YRnnjoiSV-U?feature=oembed" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen="" name="fitvid0"></iframe></p></div></figure><p>For those of you of the nerdy persuasion, here’s the original code as it appeared in the <a href="https://archive.org/details/73-magazine-1984-05/mode/2up" target="_blank">May 1984 edition</a> of “73 Magazine” and was copied to look cool on screen in The Terminator.</p><figure><img src="https://blog-cloudflare-com-assets.storage.googleapis.com/2020/04/dr-digital-cropped.png"></figure><p>If you want to scale your own COBOL-implemented Skynet, it only takes a few steps to convert COBOL to WebAssembly and have it run in over 200 cities worldwide on Cloudflare’s network.</p><h3 id="procedure-division-">PROCEDURE DIVISION.</h3><p>Here’s how you can take your COBOL program and turn it into a Worker.</p><p>There are multiple compiler implementations of the COBOL language and a few of them are proprietary. We decided to use GnuCOBOL (formerly OpenCOBOL) because it's free software.</p><p>Given that Cloudflare Workers supports WebAssembly, it sounded quite straightforward: GnuCOBOL can compile COBOL to C and Emscripten compiles C/C++ to WebAssembly. However, we need to make sure that our WebAssembly binary is as small and fast as possible to maximize the time for user-code to run instead of COBOL's runtime.</p><p>GnuCOBOL has a runtime library called libcob, which implements COBOL's runtime semantics, using GMP (GNU Multiple Precision Arithmetic Library) for arithmetic. After we compiled both these libraries to WebAssembly and linked against our compiled COBOL program, we threw the WebAssembly binary in a Cloudflare Worker.</p><p>It was too big and it hit the CPU limit (you can find <a href="https://developers.cloudflare.com/workers/about/limits/#cpu-execution-time-limit" target="_blank">Cloudflare Worker’s limits</a> here), so it was time to optimize.</p><p>GMP turns out to be a big library, but luckily for us someone made an optimized version for JavaScript (<a href="https://github.com/kripken/gmp.js" target="_blank">https://github.com/kripken/gmp.js</a>), which was much smaller and reduced the WebAssembly instantiation time. As a side note, it's often the case that functions implemented in C could be removed in favour of a JavaScript implementation already existing on the web platform. But for this project we didn’t want to rewrite GMP.</p><p>While Emscripten can emulate a file system with all its syscalls, it didn't seem necessary in a Cloudflare Worker. We patched GnuCOBOL to remove the support for local user configuration and other small things, allowing us to remove the emulated file system.</p><p>The size of our Wasm binary is relatively small compared to other languages. For example, around 230KB with optimization enabled for the Game of Life later in this blog post.</p><p>Now that we have a COBOL program running in a Cloudflare Worker, we still need a way to generate an HTTP response.</p><p>The HTTP response generation and manipulation is written in JavaScript (for now... some changes to WebAssembly are currently being discussed that would allow a better integration). Emscripten imports these functions and makes them available in C, and finally we link all the C code with our COBOL program. COBOL already has good interoperability with C code.</p><p>As an example, we implemented the rock-paper-scissors game (<a href="https://github.com/cloudflare/cobol-worker/blob/master/src/worker.cob" target="_blank">https://github.com/cloudflare/cobol-worker/blob/master/src/worker.cob</a>). See the full source (<a href="https://github.com/cloudflare/cobol-worker" target="_blank">https://github.com/cloudflare/cobol-worker</a>).</p><!--kg-card-begin: html--><!--kg-card-end: html--><p>Our work can be used by anyone wanting to compile COBOL to WebAssembly; the toolchain we used is available on GitHub (<a href="https://github.com/cloudflare/cobaul" target="_blank">https://github.com/cloudflare/cobaul</a>) and is free to use.</p><p>To deploy your own COBOL Worker, you can run the following commands. Make sure that you have wrangler installed on your machine (https://github.com/cloudflare/wrangler).</p><p><code>wrangler generate cobol-worker <a href="https://github.com/cloudflare/cobol-worker-template" target="_blank">https://github.com/cloudflare/cobol-worker-template</a></code></p><p>It will generate a cobol-worker directory containing the Worker. Follow the instructions in your terminal to configure your Cloudflare account with wrangler.</p><p>Your worker is ready to go; enter <code>npm run deploy</code> and once deployed the URL will be displayed in the console.</p><h3 id="stop-run-">STOP RUN.</h3><p>I am very grateful to Sven Sauleau for doing the work to make it easy to port a COBOL program into a Workers file and for writing the PROCEDURE DIVISION section above and to Dane Knecht for suggesting Conway’s Game of Life.</p><p>Cloudflare Workers with WebAssembly is an easy-to-use serverless platform that’s fast and cheap and scalable. It supports a wide variety of languages--including COBOL (and C, C++, Rust, Go, JavaScript, etc.). Give it a try today.</p><h3 id="afterword">AFTERWORD</h3><p>We learnt the other day of the death of <a href="https://en.wikipedia.org/wiki/John_Horton_Conway" target="_blank">John Conway</a> who is well known for <a href="https://en.wikipedia.org/wiki/Conway%27s_Game_of_Life" target="_blank">Conway’s Game of Life</a>. In tribute to Conway, XKCD <a href="https://xkcd.com/2293/" target="_blank">dedicated a cartoon</a>:</p><!--kg-card-begin: html--><center><img src="https://blog-cloudflare-com-assets.storage.googleapis.com/2020/04/image4-3.gif" alt="" width="500" height="657"></center><!--kg-card-end: html--><p>I decided to implement the Game of Life in COBOL and reproduce the cartoon.</p><!--kg-card-begin: html--><!--kg-card-end: html--><p>Here’s the code:</p><!--kg-card-begin: markdown--><pre><code>IDENTIFICATION DIVISION.
       PROGRAM-ID. worker.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01 PARAM-NAME PIC X(7).
       01 PARAM-VALUE PIC 9(10).
       01 PARAM-OUTPUT PIC X(10).
       01 PARAM PIC 9(10) BINARY.
       01 PARAM-COUNTER PIC 9(2) VALUE 0.
       01 DREW PIC 9 VALUE 0.
       01 TOTAL-ROWS PIC 9(2) VALUE 20.
       01 TOTAL-COLUMNS PIC 9(2) VALUE 15.
       01 ROW-COUNTER PIC 9(2) VALUE 0.
       01 COLUMN-COUNTER PIC 9(2) VALUE 0.
       01 OLD-WORLD PIC X(300).
       01 NEW-WORLD PIC X(300).
       01 CELL PIC X(1) VALUE "0".
       01 X PIC 9(2) VALUE 0.
       01 Y PIC 9(2) VALUE 0.
       01 POS PIC 9(3).
       01 ROW-OFFSET PIC S9.
       01 COLUMN-OFFSET PIC S9.
       01 NEIGHBORS PIC 9 VALUE 0.
       PROCEDURE DIVISION.
           CALL "get_http_form" USING "state" RETURNING PARAM.
	   IF PARAM = 1 THEN
	      PERFORM VARYING PARAM-COUNTER FROM 1 BY 1 UNTIL PARAM-COUNTER &gt; 30
	         STRING "state" PARAM-COUNTER INTO PARAM-NAME
	         CALL "get_http_form" USING PARAM-NAME RETURNING PARAM-VALUE
		 COMPUTE POS = (PARAM-COUNTER - 1) * 10 + 1
		 MOVE PARAM-VALUE TO NEW-WORLD(POS:10)
	      END-PERFORM
 	  ELSE
	    MOVE "000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001110000000000001010000000000001010000000000000100000000000101110000000000010101000000000000100100000000001010000000000001010000000000000000000000000000000000000000000000000000000000000000000" TO NEW-WORLD.
           PERFORM PRINT-WORLD.
           MOVE NEW-WORLD TO OLD-WORLD.
           PERFORM VARYING ROW-COUNTER FROM 1 BY 1 UNTIL ROW-COUNTER &gt; TOTAL-ROWS
               PERFORM ITERATE-CELL VARYING COLUMN-COUNTER FROM 1 BY 1 UNTIL COLUMN-COUNTER &gt; TOTAL-COLUMNS
	   END-PERFORM.
	   PERFORM PRINT-FORM.
           STOP RUN.
       ITERATE-CELL.
           PERFORM COUNT-NEIGHBORS.
	   COMPUTE POS = (ROW-COUNTER - 1) * TOTAL-COLUMNS + COLUMN-COUNTER.
           MOVE OLD-WORLD(POS:1) TO CELL.
           IF CELL = "1" AND NEIGHBORS &lt; 2 THEN
               MOVE "0" TO NEW-WORLD(POS:1).
           IF CELL = "1" AND (NEIGHBORS = 2 OR NEIGHBORS = 3) THEN
               MOVE "1" TO NEW-WORLD(POS:1).
           IF CELL = "1" AND NEIGHBORS &gt; 3 THEN
               MOVE "0" TO NEW-WORLD(POS:1).
           IF CELL = "0" AND NEIGHBORS = 3 THEN
               MOVE "1" TO NEW-WORLD(POS:1).
       COUNT-NEIGHBORS.
           MOVE 0 TO NEIGHBORS.
	   PERFORM COUNT-NEIGHBOR
	       VARYING ROW-OFFSET FROM -1 BY 1 UNTIL ROW-OFFSET &gt; 1
	          AFTER COLUMN-OFFSET FROM -1 BY 1 UNTIL COLUMN-OFFSET &gt; 1.
       COUNT-NEIGHBOR.
           IF ROW-OFFSET &lt;&gt; 0 OR COLUMN-OFFSET &lt;&gt; 0 THEN
               COMPUTE Y = ROW-COUNTER + ROW-OFFSET
               COMPUTE X = COLUMN-COUNTER + COLUMN-OFFSET
               IF X &gt;= 1 AND X &lt;= TOTAL-ROWS AND Y &gt;= 1 AND Y &lt;= TOTAL-COLUMNS THEN
	       	   COMPUTE POS = (Y - 1) * TOTAL-COLUMNS + X
                   MOVE OLD-WORLD(POS:1) TO CELL
		   IF CELL = "1" THEN
		      COMPUTE NEIGHBORS = NEIGHBORS + 1.
       PRINT-FORM.
           CALL "append_http_body" USING "&lt;form name=frm1 method=POST&gt;&lt;input type=hidden name=state value=".
	   CALL "append_http_body" USING DREW.
	   CALL "append_http_body" USING "&gt;".
	   PERFORM VARYING PARAM-COUNTER FROM 1 BY 1 UNTIL PARAM-COUNTER &gt; 30
    	       CALL "append_http_body" USING "&lt;input type=hidden name=state"
	       CALL "append_http_body" USING PARAM-COUNTER
    	       CALL "append_http_body" USING " value="
	       COMPUTE POS = (PARAM-COUNTER - 1) * 10 + 1
	       MOVE NEW-WORLD(POS:10) TO PARAM-OUTPUT
	       CALL "append_http_body" USING PARAM-OUTPUT
    	       CALL "append_http_body" USING "&gt;"
	   END-PERFORM
           CALL "append_http_body" USING "&lt;/form&gt;".
       PRINT-WORLD.
           MOVE 0 TO DREW.
           CALL "set_http_status" USING "200".
	   CALL "append_http_body" USING "&lt;html&gt;&lt;body onload='setTimeout(function() { document.frm1.submit() }, 1000)'&gt;"
	   CALL "append_http_body" USING "&lt;style&gt;table { background:-color: white; } td { width: 10px; height: 10px}&lt;/style&gt;".
           CALL "append_http_body" USING "&lt;table&gt;".
           PERFORM PRINT-ROW VARYING ROW-COUNTER FROM 3 BY 1 UNTIL ROW-COUNTER &gt;= TOTAL-ROWS - 1.
           CALL "append_http_body" USING "&lt;/table&gt;&lt;/body&gt;&lt;/html&gt;".
       PRINT-ROW.
           CALL "append_http_body" USING "&lt;tr&gt;".
           PERFORM PRINT-CELL VARYING COLUMN-COUNTER FROM 3 BY 1 UNTIL COLUMN-COUNTER &gt;= TOTAL-COLUMNS - 1.
           CALL "append_http_body" USING "&lt;/tr&gt;".
       PRINT-CELL.
	   COMPUTE POS = (ROW-COUNTER - 1) * TOTAL-COLUMNS + COLUMN-COUNTER.
	   MOVE NEW-WORLD(POS:1) TO CELL.
           IF CELL = "1" THEN
	       MOVE 1 TO DREW
               CALL "append_http_body" USING "&lt;td bgcolor=blue&gt;&lt;/td&gt;".
           IF CELL = "0" THEN
               CALL "append_http_body" USING "&lt;td&gt;&lt;/td&gt;".
</code></pre>
<!--kg-card-end: markdown--><p>If you want to run your own simulation you can do an HTTP POST with 30 parameters that when concatenated form the layout of the 15x20 world simulated in COBOL.</p><p>If you want to install this yourself, take the following steps:</p><!--kg-card-begin: markdown--><ol>
<li>Sign up for Cloudflare</li>
<li>Sign up for a <a href="https://workers.dev/" target="_blank">workers.dev</a> subdomain. I've already grabbed <code>cobol.workers.dev</code>, but imagine you’ve managed to grab <code>my-cool-name.workers.dev</code></li>
<li>Install <a href="https://github.com/cloudflare/wrangler" target="_blank">wrangler</a>, Cloudflare’s CLI for deploying Workers</li>
<li>Create a new COBOL Worker using the template<pre><code>wrangler generate cobol-worker https://github.com/cloudflare/cobol-worker-template
</code></pre>
</li>
<li>Configure <code>wrangler.toml</code> to point to your account and set a name for this project, let’s say <code>my-first-cobol</code>.</li>
<li>Grab the files <code>src/index.js</code> and <code>src/worker.cob</code> from my repo here: <a href="https://github.com/jgrahamc/game-of-life" target="_blank">https://github.com/jgrahamc/game-of-life</a> and replace them in the <code>cobol-worker</code>.</li>
<li><code>npm run deploy</code></li>
<li>The COBOL Worker will be running at <code>https://my-first-cobol.my-cool-name.workers.dev/</code></li>
</ol>
<!--kg-card-end: markdown-->
            </div>
        </section></div></div>
    </div>
    <footer>
        <div>created by buildstarted &copy; 2020 <a href="/about">about</a></div>
        <div>Share this page on social media: copy and paste this url https://linksfor.dev/</div>
        <div>If you prefer RSS: <a href="https://linksfor.dev/feed.xml">https://linksfor.dev/feed.xml</a></div>
        <div>Customer satisfaction guaranteed to be optional.</div>
    </footer>
    
    <script async defer>
        _dna = window._dna || {};
        _dna.siteId = "linksfor.devs";
        _dna.outlink = true;

        (function() {
            let dna = document.createElement('script');
            dna.type = 'text/javascript';
            dna.async = true;
            dna.src = '//dna.buildstarted.com/t.js';
            let s = document.getElementsByTagName('script')[0];
            s.parentNode.insertBefore(dna, s);
        })();
    </script>
    <noscript><img src="//dna.buildstarted.com/g?siteId=linksfor.devs"/></noscript>
</body>
</html>